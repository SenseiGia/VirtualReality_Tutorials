# Tutorial 00: Processing VR Overview
Note: Before progressing with this tutorial, ensure you followed the steps to prepare both your hardware and software explained at the top level of this repository.

## Create your project
When you open Processing, you will be presented with an empy workspace/sketch. Start off by saving the file, which will allow to choose location and name. The name of the sketch will become the name of the application once it has been flashed onto the phone.  

## The Various Sections of a Processing VR application
Processing is a flexible software sketchbook that allows for easy programming of complex visuals. If we were to examine the code used for Virtual Reality applications generated within Java or Kotlin from Android Studio, we would find that the code is complex with trying to create objects and scenes with OpenGL. OpenGL is a graphics library used to allow GPU rendering of shapes and objects in virtual world. 

Processing has cut out much of the complex parts and simplified it down to two major parts, allowing developers to focus more on content rather than limitations of complex code handling. The two parts consist of:
* Setup()
* Draw()

### Setup
The setup() part of the VR application allows for complete configuration of the application. This usually handles the constants of the application such as wether the application will use a monoscopic view, or stereoscopic view. It can also be used to load files, such as .obj (Waveform) files for use throughout the application. This fuction is called once at startup, useful for loading and configuring.

```Javascript
/** Used to configure the example */
void setup(){
    /* Example code to make a Stereotopic VR application */
    fullScreen(STEREO);
}
```

### Draw
The draw() part of the VR application allows for creation of visuals in the Virtual Reality enviornment. From here, you can select which objects to render and modify the view provided by either the Stereostopic or Monoscopic view. In the code block provided below, we create a simple application the interfaces with the multiple views generated by the vr application. This function is called continuously to update the renedered objects.

```Javascript
import processing.vr.*;  // Virtual Reality Library

/** Used to configure the example */
void setup() {
    /* Create a sterotopic View */
    fullScreen(STEREO);
    //fullScreen(MONO);
}

/** Used to create objects in 3D space with background */
void draw() {
  PGraphicsVR pvr = (PGraphicsVR)g;
  if (pvr.eyeType == PVR.LEFT) {
    background(200, 50, 50);
  } else if (pvr.eyeType == PVR.RIGHT) {
    background(50, 50, 200);
  } else if (pvr.eyeType == PVR.MONOCULAR) {
    background(50, 200, 50);
  }
}
```

### MousePressed
The mousePressed() part of the VR application is an optional part to a virtual reality application creation prcoess. It allows for interaction between the user of the application and the environment itself. You can use this load new objects, move objects, and change anything on touchscreeen input. The following code block builds upon the previous and interts the color of the backgrounds if the the screen is pressed. This function is called on screen presses. 

```Javascript
import processing.vr.*;  // Virtual Reality Library

/** Used to configure the example */
void setup() {
    /* Create a sterotopic View */
    fullScreen(STEREO);
    //fullScreen(MONO);
}

/** Used to create objects in 3D space with background */
void draw() {
  PGraphicsVR pvr = (PGraphicsVR)g;
  if (pvr.eyeType == PVR.LEFT) {
    background(200, 50, 50);
  } else if (pvr.eyeType == PVR.RIGHT) {
    background(50, 50, 200);
  } else if (pvr.eyeType == PVR.MONOCULAR) {
    background(50, 200, 50);
  }
}

/** Used to provide user ineteractions */
void mousePressed() {
  PGraphicsVR pvr = (PGraphicsVR)g;
  if (pvr.eyeType == PVR.LEFT) {
    background(50, 50, 200);
  } else if (pvr.eyeType == PVR.RIGHT) {
    background(200, 50, 50);
  } else if (pvr.eyeType == PVR.MONOCULAR) {
    background(200, 50, 200);
  }
}
```

## Overview Code
```Javascript
import processing.vr.*;

PShape grid;
PShape cubes;

void setup() {
  fullScreen(STEREO);
  
  grid = createShape();
  grid.beginShape(LINES);
  grid.stroke(255);
  for (int x = -10000; x < +10000; x += 250) {
    grid.vertex(x, +1000, +10000);
    grid.vertex(x, +1000, -10000);
  }
  for (int z = -10000; z < +10000; z += 250) {
    grid.vertex(+10000, +1000, z);
    grid.vertex(-10000, +1000, z);      
  }  
  grid.endShape();  
  
  cubes = createShape(GROUP);
  for (int i = 0; i < 100; i++) {
    float x = random(-1000, +1000); 
    float y = random(-1000, +1000);
    float z = random(-1000, +1000);    
    float r = random(50, 150);
    PShape cube = createShape(BOX, r, r, r);
    cube.setStroke(false);
    cube.setFill(color(180));
    cube.translate(x, y, z);
    cubes.addChild(cube);
  }
}

void draw() {
  background(0);
  lights();
  translate(width/2, height/2);
  shape(cubes);
  shape(grid);
}
```

## Create simple objects in 3D space
Now that we have esablished the basic parts to a Processing Virtual Reality application, we can now start focusing on the application and create various models and shapes within. Progress through the following tutorials in the main repository to learn more in depth details about the creation process.
